buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.+', changing: true
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

version = parent.version

def minecraft_version = '1.19.2'
def next_minecraft_minor_version = '1.20'
def fml_version = '43'
def forge_version = '43.1.1'

apply plugin: 'net.minecraftforge.gradle'

java {
    archivesBaseName = "tracker-forge-${minecraft_version}"
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

tasks.withType(JavaCompile) {
    // override, compile targeting J17
    options.release = 17
}

tasks.register('run-forge-client') {
    dependsOn 'runClient'
}

minecraft {
    // The mappings can be changed at any time and must be in the following format.
    // Channel:   Version:
    // snapshot   YYYYMMDD   Snapshot are built nightly.
    // stable     #          Stables are built at the discretion of the MCP team.
    // official   MCVersion  Official field/method names from Mojang mapping files
    //
    // You must be aware of the Mojang license when using the 'official' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'official', version: minecraft_version

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/removed as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            mods {
                tracker {
                    sourceSets.main
                }
            }
        }

    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    // Specify the version of Minecraft to use. If this is any group other than 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency, and its patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    shade project(':tracker-common')

    // Real mod deobf dependency examples - these get remapped to your current mappings
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api") // Adds JEI API as a compile dependency
    // runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}") // Adds the full JEI mod as a runtime dependency
    // implementation fg.deobf("com.tterrag.registrate:Registrate:MC${mc_version}-${registrate_version}") // Adds registrate as a dependency

    // Examples using mod jars from ./libs
    // implementation fg.deobf("blank:coolmod-${mc_version}:${coolmod_version}")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand (
                'version': project.version,
                'fml_version': fml_version,
                'forge_version': forge_version,
                'minecraft_version': minecraft_version,
                'next_minecraft_minor_version': next_minecraft_minor_version
        )
    }
}

shadowJar {
    archiveBaseName.set("tracker-forge-${minecraft_version}")
    archiveVersion.set("${project.version}")
    archiveClassifier.set('release')

    configurations = [project.configurations.shade]

    exclude 'module-info.class'

    // Several of our dependencies litter some stuff in here we don't need
    exclude 'META-INF/maven/**'
    exclude 'META-INF/versions/**/*'
    exclude '**/module-info.class'
    exclude 'mozilla/**'
    exclude 'mime.types'
    exclude 'VersionInfo.java'

    dependencies {
        exclude(dependency('commons-codec::'))
        exclude(dependency('commons-logging::'))
        exclude(dependency('org.apache.httpcomponents::'))
        exclude(dependency('org.apache.logging.log4j::'))
        exclude(dependency('org.slf4j::'))
    }
}

reobf {
    shadowJar {}
}

jar.finalizedBy('reobfJar')
tasks.build.dependsOn shadowJar

artifacts {
    archives shadowJar
    shadow shadowJar
}
